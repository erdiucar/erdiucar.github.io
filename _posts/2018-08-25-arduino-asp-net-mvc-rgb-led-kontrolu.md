---
title: 'Arduino ve ASP.NET MVC ile RGB Led KontrolÃ¼ Projesi'
date: '2018-08-25T16:16:02+03:00'
description: Projemde, Arduino' ya internet sitesi Ã¼zerinden eriÅŸerek RGB Led kontrol
  ediyorum. Web sitesini ASP.NET MVC ile yaptÄ±m. Entity Framework ve LINQ kullandÄ±m.
layout: post
permalink: /arduino-asp-net-mvc-rgb-led-kontrolu/
image: /assets/img/posts/2018/08/Arduino-asp-net-mvc-rgb-led-825x510.jpg
categories:
  - Programlama
tags:
  - arduino
  - 'asp.net mvc'
  - 'internet of things'
  - iot
  - 'nesnelerin interneti'
  - 'rgb led'
---

## Arduino ile Nesnelerin Ä°nterneti (Internet of Things - IoT)

GÃ¼nÃ¼mÃ¼z teknolojisi sayesinde internete baÄŸlÄ± olan nesneler, hayatlarÄ±mÄ±zda yer edinmeye baÅŸladÄ±. AkÄ±llÄ± nesneler ÅŸu an endÃ¼stride, ev ve binalarÄ±n otomasyonunda, ulaÅŸÄ±mda, Ã§evre analizinde, enerji ve saÄŸlÄ±k sektÃ¶rÃ¼nde yoÄŸun bir ÅŸekilde kullanÄ±lÄ±yor. Arduino ve Raspberry Pi gibi kartlarÄ± programlayarak, siz de nesneleri internete baÄŸlayÄ±p kontrol edebilirsiniz. Arduino'nun Ã§eÅŸit Ã§eÅŸit modeli var. Arduino'nun ethernet kartÄ± olmayan versiyonlarÄ±nda, internet baÄŸlantÄ±sÄ± iÃ§in Ethernet Shield almanÄ±z gerekiyor. Raspberry Pi iÃ§in bÃ¶yle bir zorunluluk yok Ã§Ã¼nkÃ¼ Ã¼zerinde ethernet kartÄ± var.

![Arduino ile IoT](/assets/img/posts/2018/08/iot.png)

## Projeyi NasÄ±l YaptÄ±m?

Bu projemde, Arduino'ya internet sitesi Ã¼zerinden eriÅŸerek RGB Led kontrol ediyorum. Ã–nce internet sitesi Ã¼zerinde Hex kodunu kÄ±rmÄ±zÄ±, yeÅŸil ve mavi renk olarak ayÄ±rÄ±yorum. Led'i aÃ§acaksam, karta mesajÄ± "kÄ±rmÄ±zÄ± + yeÅŸil + mavi + hex kodu" olarak gÃ¶nderiyorum. Kapayacaksam "Kapat" mesajÄ± gÃ¶nderiyorum. Arduino iÃ§inde bu gelen mesaja bakÄ±yorum. EÄŸer mesaj renk aÃ§mayla ilgiliyse, mesajÄ± tekrar renklere ayÄ±rÄ±p Led'i yakÄ±yorum. "Kapat" mesajÄ± geldiyse Led'i kapÄ±yorum. AyrÄ±ca Arduino'dan, baÄŸlanan siteye RGB Led'in durumuyla ilgili mesaj gÃ¶nderiyorum. AynÄ± rengin tekrar aÃ§Ä±lmasÄ± istendiyse Led'i ellemiyorum ve siteye "Renk aynÄ±" mesajÄ± gÃ¶nderiyorum. KapalÄ±yken kapanmasÄ± istendiyse, siteye "KapalÄ±" mesajÄ± gÃ¶nderiyorum. Ä°nternet sitesine gelen mesaja bakarak, Arduino'nun hangi tarih-saatte aÃ§Ä±lÄ±p kapandÄ±ÄŸÄ±nÄ±n ve hangi renk olduÄŸunun bilgisini veri tabanÄ±na ekliyorum. EÄŸer aynÄ± rengi bir daha aÃ§ma mesajÄ± gÃ¶nderdiysem veya kapalÄ±yken tekrar kapama butonuna bastÄ±ysam, veri tabanÄ±na herhangi bir ekleme yapmÄ±yorum ve sayfayÄ± yeniliyorum. Ä°nternet sitesini ASP.NET MVC ile yaptÄ±m. TasarÄ±mÄ±nÄ± Bootstrap ile yaptÄ±m. Veri tabanÄ±nÄ±, Entity Framework kullanarak, *Code First* tekniÄŸiyle oluÅŸturdum. Veri sorgulama, ekleme ve Ã§Ä±karma iÅŸlemlerini LINQ Ve Entity Framework aracÄ±lÄ±ÄŸÄ±yla yaptÄ±m. Projeyi aÅŸaÄŸÄ±daki videodan izleyebilirsiniz:

[![Arduino ve ASP.NET MVC ile RGB Led KontrolÃ¼ Projesi](https://img.youtube.com/vi/wOaQA5FbZU8/hqdefault.jpg)](https://www.youtube.com/watch?v=wOaQA5FbZU8)

ğŸ‘‰ [YouTube'da Ä°zle](https://www.youtube.com/watch?v=wOaQA5FbZU8)

## Projede kullandÄ±ÄŸÄ±m malzemeler

- Arduino Uno Rev3
- Arduino Ethernet Shield
- Breadboard
- 3 x 220 â„¦ direnÃ§
- 1 x Ortak anot RGB Led
- 4 x Ä°ki ucu erkek jumper kablo

## Arduino BaÄŸlantÄ± ÅemasÄ±, Modem AyarÄ± ve Kodu

### BaÄŸlantÄ± ÅemasÄ±

![Arduino BaÄŸlantÄ± ÅemasÄ±](/assets/img/posts/2018/08/arduino-sema.png)

**Not:** RGB Led'in uzun bacaÄŸÄ± 5V'a baÄŸlanÄ±yor.

### Modem AyarÄ±

Arduino ile internet projelerinde, modemde bir IP ve Port eÅŸleÅŸtirme yapmanÄ±z gerekiyor. Ben projemde "192.168.1.13" IP'si ve 8090 portunu Arduino ile eÅŸleÅŸtirdim. TarayÄ±cÄ±nÄ±zÄ±n adres satÄ±rÄ±na "192.168.1.1" yazarak modeminizin paneline ulaÅŸabilirsiniz. Benim kullandÄ±ÄŸÄ±m modemde "GeliÅŸmiÅŸ - NAT - Port EÅŸleÅŸtirme" kÄ±smÄ±ndan ayarlama yapÄ±lÄ±yor.

### Kod

Github Ã¼zerinde incelemek iÃ§in [tÄ±klayÄ±n](https://github.com/erdiucar/Arduino/blob/master/Arduino_ile_Internet_Uzerinden_Rgb_Led_Kontrolu/Arduino_ile_Internet_Uzerinden_Rgb_Led_Kontrolu.ino).

```cpp
// KullanÄ±lacak kÃ¼tÃ¼phaneler Ã§aÄŸrÄ±lÄ±yor
#include <SPI.h>
#include <Ethernet.h>
// KÄ±rmÄ±zÄ±, yeÅŸil ve mavi renk iÃ§in sabit oluÅŸturuyorum
const int kirmizi = 6;
const int yesil = 5;
const int mavi = 3;
// Led' in son durumunu bu deÄŸiÅŸkende tutuyorum. EÄŸer kapalÄ± deÄŸilse son gelen renk hex formatÄ±nda atanÄ±yor (#000000 gibi)
String sonRenk = "KapalÄ±";
// Ethernet kalkanÄ±nÄ±n, mac ve ip adresi giriliyor. BaÅŸka adreslerle Ã§akÄ±ÅŸmadÄ±ÄŸÄ± sÃ¼rece istenilen girilebilir
byte mac[] = {0x00, 0xAA, 0xBB, 0xCC, 0xDE, 0x02};
// Ip adresi elle belirleniyor. istenirse otomatik atama da yapÄ±labilir
IPAddress ip(192,168,1,13);
// HTTP iÃ§in 8090 portunu aÃ§tÄ±ÄŸÄ±m iÃ§in onu yazÄ±yorum
EthernetServer server(8090);
// Burada gerekli Ã¶n hazÄ±rlÄ±klar yapÄ±lÄ±yor.
void setup() {
  // Bilgisayarla seri iletiÅŸim baÅŸlatÄ±lÄ±yor. Saniyede 9600 bit gÃ¶nderimi ayarlanÄ±yor
  Serial.begin(9600);
  // Ã‡Ä±kÄ±ÅŸ olarak 3,5,6 numaralÄ± pinler belirleniyor
  pinMode(kirmizi, OUTPUT);
  pinMode(yesil, OUTPUT);
  pinMode(mavi, OUTPUT);
  // Led baÅŸlangÄ±Ã§ta kapatÄ±lÄ±yor. Rgb led anot olduÄŸu iÃ§in HIGH yapÄ±nca kapanÄ±yor.
  digitalWrite(kirmizi, HIGH);
  digitalWrite(yesil, HIGH);
  digitalWrite(mavi, HIGH);
  // Ethernet belirlenen ip'de Ã§alÄ±ÅŸtÄ±rÄ±lÄ±yor
  Ethernet.begin (mac, ip);
  // Sunucu baÅŸlatÄ±lÄ±yor
  server.begin();
}
// DÃ¶ngÃ¼ baÅŸlatÄ±lÄ±yor
void loop() {
  // Ä°stemcinin baÄŸlanmasÄ±nÄ± bekleniyor
  EthernetClient client = server.available();
  // EÄŸer istemci baÄŸlandÄ±ysa bu bÃ¶lÃ¼m Ã§alÄ±ÅŸÄ±yor
  if (client) {
    String gelen = "";
    // Ä°stemci baÄŸlÄ± olduÄŸu sÃ¼rece bu dÃ¶ngÃ¼ Ã§alÄ±ÅŸÄ±yor
    while (client.connected()) {
      if (client.available()) {
        // Arduino'ya baÄŸlanan bilgisayardan gelen mesajÄ±n karakteri okunup c deÄŸiÅŸkenine atanÄ±yor
        char c = client.read();
        // Seri monitÃ¶rde gelen karakterleri sÄ±rayla yazdÄ±rÄ±yorum
        Serial.print(c);
        // Ä°stemciden gelen karakterler "gelen" isimli stringe ekleniyor
        gelen += c;
        // EÄŸer boÅŸ bir satÄ±rdan sonra tekrar boÅŸ bir satÄ±ra denk gelinirse HTTP isteÄŸi sonlanmÄ±ÅŸ demektir
        if (c == '\n') {
          // Ä°stemciden gelen mesaj "Kapat" ise led kapanÄ±yor ve sonRenk deÄŸiÅŸkeni "KapalÄ±" oluyor.
          if (gelen == "Kapat\n") {
            client.print(sonRenk);
            sonRenk = ledKapa();
            //Serial.println(sonRenk);
          } else {
            // Renk aynÄ± mÄ± diye kontrol etmek amaÃ§lÄ± bir deÄŸiÅŸken oluÅŸturuyorum
            String kontrol = renkAyarla(gelen);
            // EÄŸer renk aynÄ±ysa sonRenk aynÄ± kalÄ±yor ve istemciye "Renk aynÄ±" diye mesaj gÃ¶nderiliyor
            if (kontrol == "Renk aynÄ±") {
              client.print("Renk aynÄ±");
              //Serial.println(sonRenk);
              //Serial.println("Renk aynÄ±");
            }
            // DeÄŸilse sonRenk'e yeni gelen renk atanÄ±yor ve istemciye en son gÃ¶nderdiÄŸi renk gÃ¶nderiliyor
            else {
              sonRenk = kontrol;
              client.print(sonRenk);
              //Serial.println(sonRenk);
            }
          }
          break;
        }
      }
    }
    // Web tarayÄ±cÄ±ya verileri almasÄ± iÃ§in 1 ms zaman veriliyor
    delay(1);
    // Ä°stemciyle baÄŸlantÄ± sonlandÄ±rÄ±lÄ±yor
    client.stop();
  }
}
// Bu metodla rgb led'in renkleri ayarlanÄ±yor
void setColor(int red, int green, int blue) {
  analogWrite(kirmizi, red);
  analogWrite(yesil, green);
  analogWrite(mavi, blue);
}
// Bu metodla rgb led kapanÄ±yor ve "KapalÄ±" mesajÄ± dÃ¶ndÃ¼rÃ¼yor
String ledKapa() {
  digitalWrite(kirmizi, HIGH);
  digitalWrite(yesil, HIGH);
  digitalWrite(mavi, HIGH);
  return "KapalÄ±";
}
// Rgb led anot olduÄŸu iÃ§in renk dÃ¼zenlemesi yapmak gerekiyor
int renkDuzenle (int renk) {
  return (255 - renk);
}
// Bu metodla gelen mesaj parÃ§alara ayrÄ±lÄ±yor. Ä°stemciye gÃ¶nderilecek renk belirleniyor
String renkAyarla(String s) {
  String red = "";
  String green = "";
  String blue = "";
  String gonderilecekRenk = "";
  byte sayac = 1;
  // Gelen mesajÄ±n tÃ¼m karakterleri kontrol ediliyor. '+' karakteri gÃ¶rÃ¼lÃ¼rse sayaÃ§ arttÄ±rÄ±larak diÄŸer renge atama yapÄ±lÄ±yor. '\n' ise dÃ¶ngÃ¼ sonlandÄ±rÄ±lÄ±yor.
  for(int i = 0, r = s.length(); i < r; i++) {
    if (s[i] == '+') {
      sayac++;
      continue;
    } else if (s[i] == '\n') {
      break;
    } else {
      if (sayac == 1) {
        red += s[i];
      } else if (sayac == 2) {
        green += s[i];
      } else if (sayac == 3) {
        blue += s[i];
      } else {
        gonderilecekRenk += s[i];
      }
    }
  }
  // sonRenk deÄŸiÅŸkeniyle gelen renk aynÄ± ise, "Renk aynÄ±" mesajÄ± dÃ¶ndÃ¼rÃ¼lÃ¼yor
  if (gonderilecekRenk == sonRenk) {
    return "Renk aynÄ±";
  }
  // Renk aynÄ± deÄŸilse yeni renk ayarlanÄ±yor ve bu yeni renk dÃ¶ndÃ¼rÃ¼lÃ¼yor
  else {
    setColor(renkDuzenle(red.toInt()), renkDuzenle(green.toInt()), renkDuzenle(blue.toInt()));
    return gonderilecekRenk;
  }
}
```

## ASP.NET MVC Anasayfa, Veri TabanÄ± EkranÄ± ve Kodu

### Ana Sayfa

![Ana Sayfa](/assets/img/posts/2018/08/ana-sayfa.png)

### Veri TabanÄ± EkranÄ±

![Veri tabanÄ± ekranÄ±](/assets/img/posts/2018/08/veri-tabani.png)

### Kodlar

Model, View ve Controller kodlarÄ± burada Ã§ok yer kaplayacaÄŸÄ± iÃ§in Github sayfa linklerini verdim.

**Model:**

- [Database modeli](https://github.com/erdiucar/ASP.NET-MVC/blob/master/RgbLedMvc/RgbLedMvc/Models/RgbLedDb.cs)
- [DiÄŸer model](https://github.com/erdiucar/ASP.NET-MVC/blob/master/RgbLedMvc/RgbLedMvc/Models/NewViewModel.cs)

**View:**

- [Ana sayfa](https://github.com/erdiucar/ASP.NET-MVC/blob/master/RgbLedMvc/RgbLedMvc/Views/Home/Index.cshtml)
- [Veri tabanÄ± ekranÄ±](https://github.com/erdiucar/ASP.NET-MVC/blob/master/RgbLedMvc/RgbLedMvc/Views/VeriTabani/Index.cshtml)

**Controller:**

- [Ana sayfa](https://github.com/erdiucar/ASP.NET-MVC/blob/master/RgbLedMvc/RgbLedMvc/Controllers/HomeController.cs)
- [Veri tabanÄ± ekranÄ±](https://github.com/erdiucar/ASP.NET-MVC/blob/master/RgbLedMvc/RgbLedMvc/Controllers/VeriTabaniController.cs)

---

Projeyi indirmek isterseniz:

- Arduino iÃ§in [tÄ±klayÄ±n](https://github.com/erdiucar/Arduino) ("Arduino_ile_Internet_Uzerinden_Rgb_Led_Kontrolu" adlÄ± klasÃ¶r)
- ASP.NET MVC iÃ§in [tÄ±klayÄ±n](https://github.com/erdiucar/ASP.NET-MVC) ("RgbLedMvc" adlÄ± klasÃ¶r)

DiÄŸer yazÄ±larÄ±mÄ± incelemek iÃ§in tÄ±klayÄ±n => [Erdi UÃ§ar](https://www.erdiucar.com)
